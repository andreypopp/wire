'use strict'

((root, modulename, factory) ->
  if typeof define === 'function' and define.amd
    define ->
      root.wire = root.wire or {}
      root.wire[modulename] = factory()
  else
    root.wire = root.wire or {}
    root.wire[modulename] = factory())(this, 'View', ->

  eventSplitterRe = /\s+/

  specialAttributes = ['events']

  class View extends wire.Base
    @mixin(wire.Event)

    tag: 'div'

    (options) ->
      for key, value in options
        if specialAttributes.indexOf(key) is -1
            this[key] = value

      if not this.el
        this.el = $(document.createElement(this.tag))

      events = options?.events or {}
      if this.events
        wire.extend(events, this.events or {})
      if events
        this.configureEvents(events)

      this.events = events

    # Configure events of a view.
    #
    # @param {Object} events
    #   The event declaration table, keys can be of form
    #
    #     a '<eventname> <dom selector>'
    #       in that case there will be event handler attached to event
    #       <eventname> on DOM element selected by <dom selector>
    #
    #     b 'eventname this.<path>'
    #       in that case there will be event handler attached to event
    #       <eventname> on this.<path> attribute of a view
    #
    #   Values can be of form
    #
    #     a function () { ... }
    #       in that case function will be called on event
    #
    #     b '<functionname>'
    #       function will be looked up on View and bound to it and then called
    #       on event
    #
    #     c 'trigger <eventname>'
    #       event <eventname> will be fired on view on event
    #       this allows event propagation
    #
    configureEvents: (events) ->
      for k, v in events
        if not (typeof v is 'function')
          if v.slice(0, 8) is 'trigger '
            v = let
              ~> this.trigger(v.slice(8))
          else
            v = let
              ~> this[v].apply(this, arguments)
        [ev, path] = k.split(eventSplitterRe)
        if path is undefined
          this.el.bind(ev, v)
        else if path.slice(0, 5) is 'this.'
          wire.path(this, path.slice(5)).on(ev, v)
        else
          this.el.on(ev, path, v)

    # Scoped DOM selector
    #
    # @param {String} selector
    $: (selector) ->
      $(selector, this.el)

    dispose: ->
      this.el.remove()
      this.off()
      # TODO what about DOM events?

  View
)
